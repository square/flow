buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 27,
      'supportLibrary': '27.0.2'
  ]

  ext.deps = [
    'support': [
      'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
      'appcompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
      'design': "com.android.support:design:${versions.supportLibrary}",
      'test': [
          'espresso': 'com.android.support.test.espresso:espresso-core:3.0.1',
          'runner': 'com.android.support.test:runner:1.0.1',
          'rules': 'com.android.support.test:rules:1.0.1',
      ]
    ],
    'junit': 'junit:junit:4.12',
    'assertj': 'org.assertj:assertj-core:3.8.0',
    'mockito': 'org.mockito:mockito-core:2.13.0',
    'robolectric': 'org.robolectric:robolectric:3.4.2',
    'antTasks': 'org.apache.maven:maven-ant-tasks:2.1.3',
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:3.0.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
  }

  repositories {
    jcenter()
    google()
  }
}

apply plugin: 'com.github.ben-manes.versions'

subprojects {
  repositories {
    jcenter()
    google()
  }

  afterEvaluate { project ->
    android {
      lintOptions {
        quiet false
        textReport true
        textOutput 'stdout'
      }
    }
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all of the primary support libraries to use the same version.
        if (details.requested.group == 'com.android.support') {
          details.useVersion versions.supportLibrary
        }
      }
    }
  }
}
